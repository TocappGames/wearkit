plugins {
    id 'com.android.library'
    id 'maven-publish'
}

group = 'dev.wearkit'

task javadocs(type: Javadoc) {
    afterEvaluate {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        classpath += files(android.libraryVariants.collect { variant ->
            variant.getJavaCompileProvider().get().classpath.files
        })
        exclude '**/R.html', '**/R.*.html', '**/index.html'
    }
}

task javadocJar(type: Jar, dependsOn: javadocs) {
    archiveClassifier.set('javadoc')
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId = group
                version = System.getProperty("args")
                from components.release
                artifact sourcesJar
                artifact javadocJar
            }
        }
        repositories {
            maven {
                url "$buildDir/outputs/maven"
            }
        }
    }
}

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 31

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation ('androidx.appcompat:appcompat:1.3.1') {transitive = true}
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation ('org.dyn4j:dyn4j:4.2.0') {transitive = true}
    implementation ('com.google.android.support:wearable:2.8.1') {transitive = true}
    compileOnly 'com.google.android.wearable:wearable:2.8.1'
    implementation 'org.yaml:snakeyaml:1.26'
}
